buildscript {
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id "org.jetbrains.dokka" version "0.9.18"
    id "base"
    id "maven-publish"
}

apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'

def GROUP_ID="com.mrcoto"
def ARTIFACT_ID="chilean-rut"

def BINTRAY_REPOSITORY="chilean-rut"
def BINTRAY_ORGANIZATION="mrcoto"

def ISSUE_URL="https://github.com/mrcoto/chilean-rut/issues"
def SITE_URL="https://github.com/mrcoto/chilean-rut"
def VCS_URL="https://github.com/mrcoto/chilean-rut.git"
def LIBRARY_VERSION_NAME=1.0

repositories {
    mavenCentral()
    jcenter()
}

group GROUP_ID
version LIBRARY_VERSION_NAME

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'test/main/kotlin'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: '1.3.41'
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-common', version: '1.3.41'
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-annotations-common', version: '1.3.41'
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: '1.3.41'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

afterEvaluate {
    project.publishing.publications.all {
        // rename artifacts
        groupId = GROUP_ID
        artifactId = "$ARTIFACT_ID"
    }
}

def getBintrayUserProperty() {
    return hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
}

def getBintrayApiKeyProperty() {
    return hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
}

bintray {
    user = getBintrayUserProperty()
    key = getBintrayApiKeyProperty()
    publish = false

    pkg {
        repo = BINTRAY_REPOSITORY
        name = ARTIFACT_ID
        userOrg = BINTRAY_ORGANIZATION
        licenses = ['MIT']
        vcsUrl = VCS_URL
        websiteUrl = SITE_URL
        issueTrackerUrl = ISSUE_URL

        version {
            name = LIBRARY_VERSION_NAME
            vcsTag = LIBRARY_VERSION_NAME
            released = new Date()
        }
    }
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.bintray.pkg.name
            from components.java

            artifact sourcesJar {
                archiveClassifier.set("sources")
            }
            artifact javadocJar {
                archiveClassifier.set("javadoc")
            }
        }
    }
}

bintrayUpload.doFirst {
    publications = publishing.publications.collect {
        it.name
    }.findAll {
        it != "kotlinMultiplatform"
    }
}

bintrayUpload.dependsOn publishToMavenLocal